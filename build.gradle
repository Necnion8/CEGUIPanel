plugins {
    id 'java'
}

group = 'com.gmail.necnionch.myplugin'
version = '1.0.0-SNAPSHOT'

configurations {
    collectJars
}

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        name = 'CodeMC'
        url = 'https://repo.codemc.org/repository/maven-public/'
    }
    maven {
        url = "https://jitpack.io"
    }

}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    implementation files("libs/CommandAPI.jar")
    implementation group: 'de.tr7zw', name: 'item-nbt-api-plugin', version: '2.8.0'

    implementation 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'
    collectJars 'net.wesjd:anvilgui:1.5.3-SNAPSHOT'

}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

jar {
    from {
        configurations.collectJars.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
